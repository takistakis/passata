#compdef passata

_passata() {
  local curcontext="$curcontext" state line
  typeset -A opt_args

  _arguments : \
    '--config[Path of the configuration file]:file:_files' \
    '(--color --no-color)--color[Colorize the output]' \
    '(--color --no-color)--no-color[Do not colorize the output]' \
    '--version[Show the version and exit]' \
    '(-h --help)'{-h,--help}'[Show help message and exit]' \
    ':command:->command' \
    '*::options:->options'

  case $state in
    command)
      local -a subcommands
      subcommands=(
        'init:Initialize password database'
        'config:Edit the configuration file'
        'ls:List entries in a tree-like format'
        'find:List matching entries in a tree-like format'
        'show:Show entry, group or the whole database'
        'insert:Insert a new password'
        'generate:Generate a random password'
        'edit:Edit entry, group or the whole database'
        'rm:Remove entries or groups'
        'mv:Move or rename entries.'
        'autotype:Type login credentials'
      )
      _describe -t commands 'command' subcommands
      ;;

    options)
      case $line[1] in
        init)
          # -s enables option stacking for single-letter options
          _arguments -s : \
            '(-f --force)'{-f,--force}'[Do not prompt for confirmation]' \
            '(-g --gpg-id)'{-g,--gpg-id}'[GnuPG ID for database encryption]:gpg key:_complete_keys' \
            '(-p --path)'{-p,--path}'[Database path]:file:_files' \
            '(-h --help)'{-h,--help}'[Show help message and exit]'
          ;;

        config)
          _arguments -s : \
            '(-e --editor)'{-e,--editor}'[Which editor to use]:string:' \
            '(-h --help)'{-h,--help}'[Show help message and exit]'
          _complete_groups
          ;;

        ls)
          _arguments -s : \
            '(-n --no-tree)'{-n,--no-tree}'[Print entries in <group>/<name> format]' \
            '(-h --help)'{-h,--help}'[Show help message and exit]'
          _complete_groups
          ;;

        find)
          _arguments -s : \
            '(-n --no-tree)'{-n,--no-tree}'[Print entries in <group>/<name> format]' \
            '(-p --print)'{-p,--print}'[Show the found entries]' \
            '(-c --clip -C --no-clip)'{-c,--clip}'[Copy password to clipboard]' \
            '(-c --clip -C --no-clip)'{-C,--no-clip}'[Print password]' \
            '(-h --help)'{-h,--help}'[Show help message and exit]'
          ;;

        show)
          _arguments -s : \
            '(-c --clip -C --no-clip)'{-c,--clip}'[Copy password to clipboard]' \
            '(-c --clip -C --no-clip)'{-C,--no-clip}'[Print password]' \
            '(-t --timeout)'{-t,--timeout}'[Number of seconds until the clipboard is cleared]:int:' \
            '(-h --help)'{-h,--help}'[Show help message and exit]'
          _complete_entries
          ;;

        insert)
          _arguments -s : \
            '(-f --force)'{-f,--force}'[Do not prompt for confirmation]' \
            '--password[Give password instead of being prompted for it]:password:' \
            '(-h --help)'{-h,--help}'[Show help message and exit]'
          _complete_entries
          ;;

        generate)
          _arguments -s : \
            '(-f --force)'{-f,--force}'[Do not prompt for confirmation]' \
            '(-p --print -P --no-print)'{-p,--print}'[Print password]' \
            '(-p --print -P --no-print)'{-P,--no-print}'[Do not print password]' \
            '(-c --clip -C --no-clip)'{-c,--clip}'[Copy password to clipboard]' \
            '(-c --clip -C --no-clip)'{-C,--no-clip}'[Do not copy password to clipboard]' \
            '(-t --timeout)'{-t,--timeout}'[Number of seconds until the clipboard is cleared]:int:' \
            '(-l --length -e --entropy)'{-l,--length}'[Length of the generated password]:int:' \
            '(-l --length -e --entropy)'{-e,--entropy}'[Calculate length for given bits of entropy]:int:' \
            '(-s --symbols -S --no-symbols -w --words -W --no-words --wordpath)'{-s,--symbols}'[Use symbols in the generated password]' \
            '(-s --symbols -S --no-symbols -w --words -W --no-words --wordpath)'{-S,--no-symbols}'[Do not use symbols in the generated password]' \
            '(-s --symbols -S --no-symbols -w --words -W --no-words --wordpath)'{-w,--words}'[Generate diceware-like passphrase]' \
            '(-s --symbols -S --no-symbols -w --words -W --no-words --wordpath)'{-W,--no-words}'[Do not generate diceware-like passphrase]' \
            '(-w --wordpath -s --symbols -S --no-symbols)'{-w,--wordpath}'[List of words for passphrase generation]:file:_files' \
            '(-h --help)'{-h,--help}'[Show help message and exit]'
          _complete_entries
          ;;

        edit)
          _arguments -s : \
            '(-e --editor)'{-e,--editor}'[Which editor to use]:string:' \
            '(-h --help)'{-h,--help}'[Show help message and exit]'
          _complete_entries
          ;;

        rm|mv)
          _arguments -s : \
            '(-f --force)'{-f,--force}'[Do not prompt for confirmation]' \
            '(-h --help)'{-h,--help}'[Show help message and exit]'
          _complete_entries
          ;;

        autotype)
          _arguments -s : \
            '(-s --sequence)'{-s,--sequence}'[Autotype sequence]:string:' \
            '(-d --delay)'{-d,--delay}'[Delay between keystrokes in ms]:int:' \
            '(-m --menu)'{-m,--menu}'[dmenu provider command]:string:' \
            '(-h --help)'{-h,--help}'[Show help message and exit]'
          ;;
      esac
      ;;
  esac
}

_complete_groups() {
  local IFS=$'\n'
  _values 'group' ${$(passata ls 2>/dev/null | grep "^\w"):-""}
}

_complete_entries() {
  local IFS=$'\n'
  _values 'entry' ${$(passata ls --no-tree 2>/dev/null):-""}
}

_complete_keys() {
  local IFS=$'\n'
  _values 'gpg key' ${$(gpg --list-secret-keys | grep -o "<.*>" | tr -d "<>"):-""}
}

_passata
